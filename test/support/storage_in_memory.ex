defmodule Engram.Storage.InMemory do
  @moduledoc """
  ETS-backed in-memory storage adapter for tests.

  Default stub for `Engram.MockStorage` — tests that need to assert on
  storage interactions still use `Mox.expect/3` directly; tests that
  just want a working backend get pass-through behaviour for free.

  Keys are namespaced per-test by including the user_id prefix already
  present in `Engram.Storage.key/3`, so cross-test collisions only
  matter when factories happen to allocate the same user_id (which they
  don't under default `insert(:user)` sequences).
  """

  @behaviour Engram.Storage

  @table :engram_test_storage_in_memory

  @doc "Lazily ensures the ETS table exists. Idempotent and safe to call concurrently."
  def ensure_table do
    case :ets.whereis(@table) do
      :undefined ->
        try do
          :ets.new(@table, [:public, :named_table, :set])
          :ok
        rescue
          ArgumentError -> :ok
        end

      _ ->
        :ok
    end
  end

  @impl true
  def put(key, binary, _opts \\ []) do
    ensure_table()
    :ets.insert(@table, {key, binary})
    :ok
  end

  @impl true
  def get(key) do
    ensure_table()

    case :ets.lookup(@table, key) do
      [{^key, binary}] -> {:ok, binary}
      [] -> {:error, :not_found}
    end
  end

  @impl true
  def delete(key) do
    ensure_table()
    :ets.delete(@table, key)
    :ok
  end

  @impl true
  def exists?(key) do
    ensure_table()
    :ets.member(@table, key)
  end
end
